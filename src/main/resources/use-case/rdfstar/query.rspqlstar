BASE <http://base/>
PREFIX ex: <http://www.example.org/ontology#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>

REGISTER STREAM <alert/lowEmergencyAbnormalState>

COMPUTED EVERY PT10S AS

SELECT ?activity (AVG(?hr) AS ?avgHr) (AVG(?br) AS ?avgBr) (AVG(?ox) AS ?avgOx)
FROM NAMED WINDOW <w1> ON <s/activity>  [RANGE PT10M STEP PT10S]
FROM NAMED WINDOW <w2> ON <s/location>  [RANGE PT30S STEP PT10S]
FROM NAMED WINDOW <w3> ON <s/heart>     [RANGE PT30S STEP PT10S]
FROM NAMED WINDOW <w4> ON <s/breathing> [RANGE PT30S STEP PT10S]
FROM NAMED WINDOW <w5> ON <s/oxygen>    [RANGE PT30S STEP PT10S]
WHERE {
    # Rut
    ?person a foaf:Person ;
       foaf:name "Rut" ;
       ex:partner ?partner .

    # Context
    [] a ex:Context ;
       ex:hasPerson ?person ;
       ex:forActivity ?activity ;
       ex:expectedHeartRate     [ ex:upperBound ?hrMax ] ;
       ex:expectedBreathingRate [ ex:lowerBound ?brMin ] ;
       ex:expectedOxygenLevel   [ ex:lowerBound ?oxMin ; ex:upperBound ?oxMax ] .

    # activity, reported by the system and assumed to be true
    WINDOW <w1> {
        GRAPH ?g1 {
            [ a sosa:Observation ;
              sosa:featureOfInterest ?person ;
              sosa:hasSimpleResult ?activity ] .
        }
    }

    # Location of Rut's partner
    WINDOW <w2> {
        GRAPH ?g2 {
            ?o2 a sosa:Observation ;
                sosa:featureOfInterest ?partner .
            <<?o2 sosa:hasSimpleResult ?loc>> ex:confidence ?c2 .
            FILTER(?c2 > 0.9 && ?loc != <person1/home>)
        }
    }

    # Heart rate
    WINDOW <w3> {
        GRAPH ?g3 {
            ?o3 a sosa:Observation ;
                sosa:featureOfInterest ?person .
            <<?o3 sosa:hasSimpleResult ?hr>> ex:confidence ?c3 .
            FILTER(?c3 > 0.95)
        }
    }
    # Breathing rate
    WINDOW <w4> {
        GRAPH ?g4 {
            ?o4 a sosa:Observation ;
                sosa:featureOfInterest ?person .
            <<?o4 sosa:hasSimpleResult ?br>> ex:confidence ?c4 .
            FILTER(?c4 > 0.95)
        }
    }
    # Oxygen level
    WINDOW <w5> {
        GRAPH ?g5 {
            ?o5 a sosa:Observation ;
                sosa:featureOfInterest ?person .
            <<?o5 sosa:hasSimpleResult ?ox>> ex:confidence ?c5 .
            FILTER(?c5 > 0.95)
        }
    }
}
GROUP BY ?activity ?hrMax ?brMin ?oxMin ?oxMax
HAVING(?avgHr > ?hrMax && ?avgBr < ?brMin &&
       ?oxMin <= ?avgOx && ?avgOx <= ?oxMax)