BASE <http://base/>
PREFIX ex: <http://www.example.org/ontology#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>

REGISTER STREAM <alert/lowEmergencyAbnormalState>

COMPUTED EVERY PT10S AS

SELECT ?activity ?hrMax ?brMin ?oxMin ?oxMax
       (AVG(?hr) AS ?avgHr) (AVG(?br) AS ?avgBr) (AVG(?ox) AS ?avgOx)
FROM NAMED WINDOW <w1> ON <s/location1> [RANGE PT3M STEP PT10S]
FROM NAMED WINDOW <w2> ON <s/location2> [RANGE PT3M STEP PT10S]
FROM NAMED WINDOW <w3> ON <s/activity>  [RANGE PT3M STEP PT10S]
FROM NAMED WINDOW <w4> ON <s/heart>     [RANGE PT3M STEP PT10S]
FROM NAMED WINDOW <w5> ON <s/breathing> [RANGE PT3M STEP PT10S]
FROM NAMED WINDOW <w6> ON <s/oxygen>    [RANGE PT3M STEP PT10S]
WHERE {
    # Rut
    ?person a foaf:Person ;
       foaf:name "Rut" ;
       ex:partner ?partner .

    # Context
    [] a ex:Context ;
       ex:hasPerson ?person1 ;
       ex:forActivity ?activity ;
       ex:expectedHeartRate     [ ex:upperBound ?hrMax ] ;
       ex:expectedBreathingRate [ ex:lowerBound ?brMin ] ;
       ex:expectedOxygenLevel   [ ex:lowerBound ?oxMin ; ex:upperBound ?oxMax ] .

    # Rut's location must match <person1/home>
    WINDOW <w1> {
        GRAPH ?g1 {
            ?o1 a sosa:Observation ;
                sosa:featureOfInterest ?person .
            <<?o1 sosa:hasSimpleResult ?loc1>> ex:confidence ?c1 .
            FILTER(?c1 > 0.9 && ?loc1 = <person1/home>)
        }
    }

    # Don't trigger alert if Rut's partner is at home
    WINDOW <w2> {
        GRAPH ?g2 {
            ?o2 a sosa:Observation ;
                sosa:featureOfInterest ?partner .
            <<?o2 sosa:hasSimpleResult ?loc2>> ex:confidence ?c2 .
            FILTER(?c2 > 0.9 && ?loc2 = ?loc2)
        }
    }

    # Activity
    WINDOW <w3> {
        GRAPH ?g3 {
            ?o3 a sosa:Observation ;
                sosa:featureOfInterest ?person .
            <<?o3 sosa:hasSimpleResult ?activity>> ex:confidence ?c3 .
            FILTER(?c3 > 0.9)
        }
    }
    # Heart rate
    WINDOW <w4> {
        GRAPH ?g4 {
            ?o4 a sosa:Observation ;
                sosa:featureOfInterest ?person .
            <<?o2 sosa:hasSimpleResult ?hr>> ex:confidence ?c4 .
            FILTER(?c4 > 0.9)
        }
    }
    # Breathing rate
    WINDOW <w5> {
        GRAPH ?g5 {
            ?o5 a sosa:Observation ;
                sosa:featureOfInterest ?person .
            <<?o5 sosa:hasSimpleResult ?br>> ex:confidence ?c5 .
            FILTER(?c5 > 0.9)
        }
    }
    # Oxygen level
    WINDOW <w6> {
        GRAPH ?g6 {
            ?o6 a sosa:Observation ;
                sosa:featureOfInterest ?person .
            <<?o6 sosa:hasSimpleResult ?ox>> ex:confidence ?c6 .
            FILTER(?c6 > 0.9)
        }
    }
}
GROUP BY ?activity ?hrMax ?brMin ?oxMin ?oxMax
HAVING(?avgHr > ?hrMax && ?avgBr < ?brMin &&
       ?oxMin <= ?avgOx && ?avgOx <= ?oxMax)